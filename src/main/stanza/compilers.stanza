defpackage firrtl/compiler :
  import core
  import verse
  import firrtl/passes
  import firrtl/errors
  import firrtl/flo
  import firrtl/verilog
  import firrtl/ir2
  import firrtl/ir-utils

public defstruct StandardFlo <: Compiler :
   file: String with: (as-method => true)
public defmethod passes (c:StandardFlo) -> List<Pass> :
   to-list $ [
      RemoveSpecialChars()
      RemoveScopes()
      CheckHighForm()
      ;; TempElimination()
      ToWorkingIR()
      ResolveKinds()
      CheckKinds()
      InferTypes()
      CheckTypes()
      ResolveGenders()
      CheckGenders()
      ExpandAccessors()
      LowerToGround()
      ExpandIndexedConnects()
      ExpandWhens()
      InferWidths()
      Pad()
      Inline()
      SplitExp()
      ToRealIR()
      RemoveSpecialChars()
      CheckHighForm()
      CheckLowForm()
      Flo(file(c))
   ]

public defstruct StandardVerilog <: Compiler :
   file: String with: (as-method => true)
public defmethod passes (c:StandardVerilog) -> List<Pass> :
   to-list $ [
      RemoveSpecialChars()
      RemoveScopes()
      CheckHighForm()
      TempElimination()
      ToWorkingIR()
      ;; MakeExplicitReset()
      ResolveKinds()
      CheckKinds()
      InferTypes()
      CheckTypes()
      ResolveGenders()
      CheckGenders()
      ExpandAccessors()
      LowerToGround()
      ExpandIndexedConnects()
      ExpandWhens()
      InferWidths()
      Pad()
      SplitExp()
      ToRealIR()
      RemoveSpecialChars()
      CheckHighForm()
      CheckLowForm()
      CheckInitialization()
      Verilog(file(c))
   ]

;============= DRIVER ======================================
public defn run-passes (c:Circuit,comp:Compiler) : 
   run-passes(c,passes(comp))
public defn run-passes (c:Circuit,ls:List<Pass>) :
   var c*:Circuit = c
   println("Compiling!")
   if PRINT-CIRCUITS : println("Original Circuit")
   if PRINT-CIRCUITS : print(c)
   ;val start-time = current-time-us()
   val start-time = to-int(to-string(current-time-us() / to-long(1000)))
   var t = start-time
   val time-table = Vector<[String,Int]>()
   for p in ls do : 
      println-all(STANDARD-ERROR,["Starting " name(p)])
      if PRINT-CIRCUITS : println(name(p))
      c* = pass(p)(c*)
      if PRINT-CIRCUITS : print(c*)
      val current-time = to-int(to-string(current-time-us() / to-long(1000)))
      println-all(STANDARD-ERROR,["Finished " name(p)])
      println-all(STANDARD-ERROR,["Milliseconds since start: " current-time - start-time])
      println-all(STANDARD-ERROR,["Milliseconds for this pass: " current-time - t])
      println-all(STANDARD-ERROR,["\n"])
      add(time-table,[name(p), current-time - t])
      t = current-time

   println(STANDARD-ERROR,"===== Time Breakdown =====")
   for x in time-table do :
      println-all(STANDARD-ERROR,[x[0] " --- " to-float(x[1] as Int) / to-float(t - start-time) "%"])
   println(STANDARD-ERROR,"==========================")
   println(STANDARD-ERROR,"Done!")
